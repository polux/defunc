-- Copyright 2018 Google LLC
--
-- Licensed under the Apache License, Version 2.0 (the "License");
-- you may not use this file except in compliance with the License.
-- You may obtain a copy of the License at
--
--      http://www.apache.org/licenses/LICENSE-2.0
--
-- Unless required by applicable law or agreed to in writing, software
-- distributed under the License is distributed on an "AS IS" BASIS,
-- WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
-- See the License for the specific language governing permissions and
-- limitations under the License.

id x = Just(x);

choice(s1, s2) t =
  match s1 t with
  | Nothing() -> s2 t
  | Just(t') -> Just(t')
  end;

seq(s1, s2) t =
  match s1 t with
  | Nothing() -> Nothing()
  | Just(t') -> s2 t'
  end;

all s t =
  match t with
  | Term(f, ts) ->
      match all' s ts with
      | Nothing() -> Nothing()
      | Just(ts') -> Just(Term(f, ts'))
      end
  end;

all' s ts =
  match ts with
  | Nil() -> Just(Nil())
  | Cons(t, ts) ->
      match s t with
      | Nothing() -> Nothing()
      | Just(t') ->
          match all' s ts with
          | Nothing() -> Nothing()
          | Just(ts') -> Just(Cons(t', ts'))
          end
      end
  end;

try s t = choice(s, id) t;

topDown s t = seq(s, all (topDown s)) t;

rs t =
  match t with
  | Term(Plus(), [Term(Z(), []), m]) -> Just(m)
  | Term(Plus(), [Term(S(), [n]), m]) -> Just(Term(S(), [Term(Plus(), [n, m])]))
  | t -> Nothing()
  end;

topDown rs Term(Z())
